" vim: set foldlevel=0 foldmethod=marker:

" Vimscripts {{{
" Ensure XDG locations exists

let g:bundle_dir = $XDG_DATA_HOME . '/vim/bundle'
let g:cache_dir = $XDG_CACHE_HOME . '/vim'
let g:config_dir = $XDG_CONFIG_HOME . '/vim'
let g:data_dir = $XDG_DATA_HOME . '/vim'
let g:undo_dir = g:cache_dir . '/undo'

if !isdirectory(g:bundle_dir) | call mkdir(g:bundle_dir) | endif
if !isdirectory(g:cache_dir) | call mkdir(g:cache_dir) | endif
if !isdirectory(g:config_dir) | call mkdir(g:config_dir) | endif
if !isdirectory(g:data_dir) | call mkdir(g:data_dir) | endif
if !isdirectory(g:undo_dir) | call mkdir(g:undo_dir) | endif

if has('nvim')
  set viminfo+=n$XDG_CACHE_HOME/nvim/shada/main.shada
else
  set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
endif
set runtimepath+=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after

runtime bundles.vim
runtime macros/matchit.vim " match the opening/closign html tag when pressing '%'
" }}}

" Vim options {{{
filetype plugin indent on
syntax on
set encoding=utf-8
set number
set relativenumber
set ignorecase
set smartcase
set infercase
set hlsearch
set incsearch
set smartindent
set tabstop=2
set softtabstop=2
set shiftwidth=2
set expandtab
set backspace=indent,eol,start
set wildmenu
set wildmode=full
set mouse=a
set nowrap
set shiftround
set laststatus=2
set nobackup
set noswapfile
let &undodir=g:undo_dir . '//'
set undofile
set undolevels=1000 "maximum number of changes that can be undone
set undoreload=10000 "maximum number lines to save for undo on a buffer reload
set dictionary=/usr/share/dict/words
set foldmethod=syntax
set nofoldenable
set history=1000
set scrolloff=3 " Leave some ground while scrolling
set background=dark
set shortmess+=c " Quiet completions
set splitright
set updatetime=250
set colorcolumn=80
set termguicolors
set modeline
set hidden
if ! has("nvim")
  set signcolumn=number
endif
set timeoutlen=500
set redrawtime=5000
if has('pythonx')
  set pyxversion=3
endif

scriptencoding utf-8

let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
colorscheme base16-monokai
" }}}

" Highlight {{{
highlight OverLength ctermbg=red ctermfg=white guibg=#592929

" Marks the 81st column of lines wider than 80 characters
function! ShowOverLength()
  if !exists('b:over_length_match')
    let b:over_length_match = matchadd('OverLength', '\v%81v\ze(.+)', 100)
  endif
endfunction

function! HideOverLength()
  if exists('b:over_length_match')
    call matchdelete(b:over_length_match)
    unlet b:over_length_match
  endif
endfunction

command! HideOverLength call HideOverLength()
command! ShowOverLength call ShowOverLength()
" }}}

augroup markdown " {{{
  autocmd!

  " Set syntax highlighting for specific file types
  autocmd BufRead,BufNewFile *.md set filetype=markdown
  " Enable spellchecking for Markdown
  autocmd FileType markdown setlocal spell
  " Automatically wrap at 80 characters for Markdown
  autocmd BufRead,BufNewFile *.md setlocal textwidth=80
augroup END " }}}

augroup git " {{{
  autocmd!

  " Automatically wrap at 72 characters and spell check git commit messages
  autocmd FileType gitcommit setlocal textwidth=72
  autocmd FileType gitcommit setlocal spell
  " Automatically closes Fugitive.vim buffers when leaving them
  autocmd BufReadPost fugitive://* set bufhidden=delete
augroup END
" }}}

" General auto commands {{{
augroup vimrcEx
  autocmd!
  " When editing a file, always jump to the last known cursor position.
  " Don't do it for commit messages, when the position is invalid, or when
  " inside an event handler (happens when dropping a file on gvim).
  autocmd BufReadPost *
        \ if &ft != 'gitcommit' && line("'\"") > 0 && line("'\"") <= line("$") |
        \ exe "normal g`\"" |
        \ endif
  " Allow stylesheets and html to autocomplete hyphenated words
  autocmd FileType html,css,scss,sass setlocal iskeyword+=-
  " Remove any trailing whitespace before saving
  autocmd BufWrite * :FixWhitespace
  " Resize splits when the window is resized
  autocmd VimResized * :wincmd =
  " Treat ? as part of names in Ruby
  autocmd FileType ruby setlocal iskeyword+=?
augroup END
" }}}

" Ag as grep {{{
if executable('ag')
  set grepprg=ag\ --nogroup\ --nocolor\ --smart-case
end
" }}}

" Ale {{{
let g:ale_echo_msg_error_str = 'E'
let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'
let g:ale_echo_msg_warning_str = 'W'
let g:ale_linters = {'javascript': [], 'typescript': []}
let g:ale_sign_column_always = 1
let g:ale_sign_error = '✗'
let g:ale_sign_warning = '⚠'
highlight link ALEErrorSign errormsg
highlight link ALEWarningSign question
" }}}

" Netrw {{{
let g:netrw_localrmdir='rm -r'
let g:netrw_home = g:data_dir
" }}}

" Vim instant markdown {{{
let g:instant_markdown_autostart = 0
" }}}

" Lightline {{{
function! LightlineFilename()
  let l:filename = ''
  if &readonly && &filetype !=# 'help'
    let l:filename .= ' '
  endif

  let l:filename .= (expand('%:t') !=# '' ? expand('%:t') : '[No Name]')

  if &modified && &filetype !=# 'help'
    let l:filename .= ' +'
  endif

  return l:filename
endfunction

function! LightlineFugitive()
  if exists('*fugitive#head')
    let l:branch = fugitive#head()
    return l:branch !=# '' ? ' '. l:branch : ''
  endif
  return ''
endfunction

function! ALEGetStatusLine() abort
  let l:counts = ale#statusline#Count(bufnr(''))

  let l:all_errors = l:counts.error + l:counts.style_error
  let l:warnings = l:counts.total - l:all_errors

  return l:counts.total == 0 ? '' : printf(
        \ '✗ %d ⚠ %d',
        \ all_errors,
        \ warnings
        \)
endfunction


function! CocStatusDiagnostic() abort
  let info = get(b:, 'coc_diagnostic_info', {})
  if empty(info) | return '' | endif

  let msgs = []
  if get(info, 'error', 0)
    call add(msgs, '✗ ' . info['error'])
  endif

  if get(info, 'warning', 0)
    call add(msgs, '⚠ ' . info['warning'])
  endif

  return join(msgs, ' ') . get(g:, 'coc_status', '')
endfunction

let g:lightline = {
  \ 'active': {
  \   'left': [ [ 'mode', 'paste' ], [ 'fugitive', 'ale', 'filename', 'cocstatus', 'currentfunction' ] ]
  \ },
  \ 'component': {
  \   'filename': '%{LightlineFilename()}',
  \   'fugitive': '%{LightlineFugitive()}',
  \   'ale': '%{ALEGetStatusLine()}',
  \   'cocstatus': '%{CocStatusDiagnostic()}',
  \ },
  \ 'component_function': {
  \   'currentfunction': 'CocCurrentFunction'
  \ },
  \ 'component_visible_condition': {
  \   'fugitive': '(exists("*fugitive#head") && ""!=fugitive#head())',
  \   'ale': "ALEGetStatusLine() != ''"
  \ },
  \ 'separator': { 'left': '', 'right': '' },
  \ 'subseparator': { 'left': '', 'right': '' }
  \ }
let g:buftabline_numbers = 1
let g:buftabline_indicators = 1
let g:buftabline_separators = 1
let g:lightline.colorscheme = 'Tomorrow_Night'
" }}}

" Switch {{{
augroup ruby
  autocmd!
  autocmd FileType ruby let b:switch_custom_definitions =
        \ [
        \   {
        \     'validates_\(\w\+\)_of\s\+\(:\w\+\)': 'validates \2, \1: true',
        \     'validates\s\+:\(\w\+\),\s\+\(\w\+\):\s\+true':     'validates_\2_of :\1',
        \   },
        \ ]
augroup end
" }}}

" Vim test {{{
let g:test#strategy = 'vtr'
" }}}

" Localvimrc  {{{
let g:localvimrc_persistent = 1
let g:localvimrc_sandbox = 0
" }}}

" FZF {{{
let g:fzf_layout = { 'window': { 'width': 0.9, 'height': 0.6  }  }

" Command to list MRU files
command! FZFMru call fzf#run({
\  'source':  v:oldfiles,
\  'sink':    'e',
\  'options': '-m -x +s',
\  'window': g:fzf_layout.window,
\ })
" }}}

" CtrlSF {{{
let g:ctrlsf_default_root = 'project'
let g:ctrlsf_leading_space = 2
let g:ctrlsf_regex_pattern = 1
" }}}

" Disable standard tComment mappings {{{
let g:tcomment_mapleader1 = ''
let g:tcomment_mapleader2 = ''
" }}}

" Yank Ring {{{
let g:yankring_dot_repeat_yank = 1
let g:yankring_clipboard_monitor = 0
let g:yankring_history_dir = g:data_dir
" }}}

" Gundo {{{
let g:gundo_prefer_python3 = 1
" }}}

" GitGutter {{{
let g:gitgutter_async = 1
let g:gitgutter_realtime = 1
" }}}

" Auto create dir on save {{{
augroup CreateDir
  au!
  au BufWritePre,FileWritePre * call <SID>CreateAndSaveDirectory()
augroup END

function s:CreateAndSaveDirectory()
  let s:directory = expand('<afile>:p:h')
  if !isdirectory(s:directory)
    call mkdir(s:directory, 'p')
  endif
endfunction
" }}}

" Terraform {{{
let g:terraform_fmt_on_save = 1
" }}}

" JS {{{
augroup jsx " {{{
  autocmd FileType javascript runtime! ftplugin/html/sparkup.html
augroup end " }}}

let g:jsx_ext_required = 0
let g:user_emmet_settings = {
      \ 'javascript': {
      \    'attribute_name': { 'class': 'className', 'for': 'htmlFor' }
      \ }
      \ }
" }}}

" Elm {{{
let g:elm_format_autosave = 1
" }}}

" YAML {{{
augroup yaml
  autocmd!
  autocmd BufRead,BufNewFile *.yml,*.yaml set filetype=ansible
augroup end
" }}}

" COC {{{
let g:coc_config_home = g:config_dir . "/coc"
let g:coc_global_extensions = [
      \ "coc-css",
      \ "coc-dictionary",
      \ "coc-eslint",
      \ "coc-go",
      \ "coc-highlight",
      \ "coc-html",
      \ "coc-java",
      \ "coc-json",
      \ "coc-lists",
      \ "coc-markdownlint",
      \ "coc-omnisharp",
      \ "coc-python",
      \ "coc-sh",
      \ "coc-snippets",
      \ "coc-solargraph",
      \ "coc-spell-checker",
      \ "coc-sql",
      \ "coc-syntax",
      \ "coc-tag",
      \ "coc-template",
      \ "coc-tslint-plugin",
      \ "coc-tsserver",
      \ "coc-vimlsp",
      \ "coc-xml",
      \ "coc-yaml",
      \ ]
" }}}

" COC FZF {{{
let g:coc_fzf_preview = ''
let g:coc_fzf_opts = []
" }}}
